import org.gradle.util.GradleVersion

allprojects {
    def getRootProperty = { String name, String defaultValue ->
        String envName = name.replaceAll(/[^\w]/, '_').toUpperCase()
        return rootProject.findProperty(name)
            ?: System.getProperty(name)
            ?: System.getenv(envName)
            ?: defaultValue
    }

    ext['gradle-api.version'] = getRootProperty('gradle-api.version', GradleVersion.current().version)

    ext['disable-compilation'] = getRootProperty('disable-compilation', 'false')
    ext['disable-tests'] = getRootProperty('disable-test', 'false')
    ext['disable-verification'] = getRootProperty('disable-verification', 'false')
    ext['disable-verification-except-tests'] = getRootProperty('disable-verification-except-tests', 'false')

    ext['repository-html-url'] = getRootProperty('repository-html-url', '')
    ext['repository-description'] = getRootProperty('repository-description', '')
    ext['repository-topics'] = getRootProperty('repository-topics', '')


    File rootProjectDir = file(rootDir)
    if (rootProjectDir.name == 'buildSrc') rootProjectDir = rootProjectDir.parentFile
    ext['rootProjectDir'] = rootProjectDir

    ext['baseJavaPackage'] = "${rootProject.group}.${rootProject.name}.v2${project.path}"
        .replace('-', '')
        .replace(':', '.')
        .replaceFirst(/\.+$/, '')
        .replaceAll(/\.{2,}/, '.')
}
